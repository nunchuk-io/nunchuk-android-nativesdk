apply from: 'configs/dependencies.gradle'
apply plugin: 'com.android.library'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-kapt'
apply plugin: 'maven-publish'

android {
    compileSdkVersion androidConfig.compileSdkVersion
    defaultConfig {
        minSdkVersion androidConfig.minSdkVersion
        targetSdkVersion androidConfig.targetSdkVersion
        externalNativeBuild {
            cmake {
                cppFlags "-std=c++11 -stdlib=libc++"
                ndk {
                    'armeabi-v7a'
                }
            }
        }
        javaCompileOptions {
            annotationProcessorOptions {
                arguments = ["library": "true"]
            }
        }
    }

    flavorDimensions 'cpuArch'
    productFlavors {
        arm8 {
            dimension 'cpuArch'
            ndk {
                abiFilters 'arm64-v8a', 'armeabi-v7a'
            }
        }
        arm64_v8a {
            dimension 'cpuArch'
            ndk {
                abiFilters 'arm64-v8a'
            }
        }
        armeabi_v7a {
            dimension 'cpuArch'
            ndk {
                abiFilters 'armeabi-v7a'
            }
        }
        x86_64 {
            dimension 'cpuArch'
            ndk {
                abiFilters 'x86_64'
            }
        }
        universal {
            dimension 'cpuArch'
            // include all default ABIs. with NDK-r16,  it is:
            //   armeabi-v7a, arm64-v8a, x86, x86_64
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    kotlinOptions {
        jvmTarget = '1.8'
    }

    externalNativeBuild {
        cmake {
            path "src/main/native/CMakeLists.txt"
            version "3.18.1"
        }
    }
}

buildscript {
    apply from: 'configs/dependencies.gradle'
    ext {
        kotlin_version = '1.6.10'
    }
    repositories {
        google()
        mavenCentral()
        maven { url "https://maven.google.com" }
        mavenLocal()
    }
    dependencies {
        classpath pluginsDeps.gradleAndroid
        classpath kotlinDeps.plugin
        classpath pluginsDeps.kotlinAndroidRuntime
    }
}

allprojects {
    repositories {
        google()
        mavenCentral()
        maven { url "https://maven.google.com" }
        mavenLocal()
    }
}

def SUPPORTED_ARCH = "arm64_v8a"
def GROUP_ID = "io.nunchuk.android"
def ARTIFACT_ID = "nativesdk-${SUPPORTED_ARCH}"
def VERSION = "1.1.45"

publishing {
    publications {
        maven(MavenPublication) {
            groupId "${GROUP_ID}"
            artifactId "$ARTIFACT_ID"
            version "${VERSION}"
            artifact("$buildDir/outputs/aar/nunchuk-android-nativesdk-${SUPPORTED_ARCH}-debug.aar")
        }
    }

    repositories {
        maven {
            url mavenLocal().getUrl()
        }
    }
}
